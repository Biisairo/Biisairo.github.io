# point shadow

방향성이 있는 spot light 나 directional light 와 다르게 point light 는 모든 방향으로 퍼져나가는 빛

일반적인 2D texture 를 사용하는 것이 아닌 cube map 을 사용해 모든 방향으로 render

```cpp
glBindTexture(GL_TEXTURE_CUBE_MAP, depthCubemap);
for (unsigned int i = 0; i < 6; ++i)
	glTexImage2D(GL_TEXTURE_CUBE_MAP_POSITIVE_X + i, 0, GL_DEPTH_COMPONENT, SHADOW_WIDTH, SHADOW_HEIGHT, 0, GL_DEPTH_COMPONENT, GL_FLOAT, NULL);
glTexParameteri(GL_TEXTURE_CUBE_MAP, GL_TEXTURE_MAG_FILTER, GL_NEAREST);
glTexParameteri(GL_TEXTURE_CUBE_MAP, GL_TEXTURE_MIN_FILTER, GL_NEAREST);
glTexParameteri(GL_TEXTURE_CUBE_MAP, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE);
glTexParameteri(GL_TEXTURE_CUBE_MAP, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE);
glTexParameteri(GL_TEXTURE_CUBE_MAP, GL_TEXTURE_WRAP_R, GL_CLAMP_TO_EDGE);
glFramebufferTexture(GL_FRAMEBUFFER, GL_DEPTH_ATTACHMENT, depthCubemap, 0);
glDrawBuffer(GL_NONE);
glReadBuffer(GL_NONE);
```

이외의 전체적인 과정은 기존과 동일

```cpp
// 1. first render to depth cubemap
glViewport(0, 0, SHADOW_WIDTH, SHADOW_HEIGHT);
glBindFramebuffer(GL_FRAMEBUFFER, depthMapFBO);
    glClear(GL_DEPTH_BUFFER_BIT);
    ConfigureShaderAndMatrices();
    RenderScene();
glBindFramebuffer(GL_FRAMEBUFFER, 0);
// 2. then render scene as normal with shadow mapping (using depth cubemap)
glViewport(0, 0, SCR_WIDTH, SCR_HEIGHT);
glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);
ConfigureShaderAndMatrices();
glBindTexture(GL_TEXTURE_CUBE_MAP, depthCubemap);
RenderScene();
```

projection 행렬은 perspective 행렬을 사용하며 fov 를 90도로 주어 한 면을 채우도록 함

```cpp
float aspect = (float)SHADOW_WIDTH/(float)SHADOW_HEIGHT;
float near = 1.0f;
float far = 25.0f;
glm::mat4 shadowProj = glm::perspective(glm::radians(90.0f), aspect, near, far);
```

큐브 맵의 각 면에 대해 view 행렬을 따로 만듬

cube map texture 에서 정의되어있는 순서대로

- right : GL_TEXTURE_CUBE_MAP_POSITIVE_X
- left : GL_TEXTURE_CUBE_MAP_NEGATIVE_X
- top : GL_TEXTURE_CUBE_MAP_POSITIVE_Y
- bottom : GL_TEXTURE_CUBE_MAP_NEGATIVE_Y
- near : GL_TEXTURE_CUBE_MAP_POSITIVE_Z
- far : GL_TEXTURE_CUBE_MAP_NEGATIVE_Z

순서로 설정

```cpp
std::vector<glm::mat4> shadowTransforms;
shadowTransforms.push_back(shadowProj * glm::lookAt(lightPos, lightPos + glm::vec3( 1.0, 0.0, 0.0), glm::vec3(0.0,-1.0, 0.0));
shadowTransforms.push_back(shadowProj * glm::lookAt(lightPos, lightPos + glm::vec3(-1.0, 0.0, 0.0), glm::vec3(0.0,-1.0, 0.0));
shadowTransforms.push_back(shadowProj * glm::lookAt(lightPos, lightPos + glm::vec3( 0.0, 1.0, 0.0), glm::vec3(0.0, 0.0, 1.0));
shadowTransforms.push_back(shadowProj * glm::lookAt(lightPos, lightPos + glm::vec3( 0.0,-1.0, 0.0), glm::vec3(0.0, 0.0,-1.0));
shadowTransforms.push_back(shadowProj * glm::lookAt(lightPos, lightPos + glm::vec3( 0.0, 0.0, 1.0), glm::vec3(0.0,-1.0, 0.0));
shadowTransforms.push_back(shadowProj * glm::lookAt(lightPos, lightPos + glm::vec3( 0.0, 0.0,-1.0), glm::vec3(0.0,-1.0, 0.0));
```

## depth shader

depth cube map 을 위해서는 vertex, geometry, fragment shader 세개의 shader 가 필요

- vertex shader
    - 정점을 light 의 공간으로 변환 후 geometry shader 로 전달
    - VERTEX 에 model 을 곱하기만 하면 됨
    
    ```cpp
    #version 400 core
    layout (location = 0) in vec3 aPos;
    
    uniform mat4 model;
    
    void main()
    {
        gl_Position = model * vec4(aPos, 1.0);
    }
    ```
    
- geometry shader
    - gl_Layer
        - 다중 레이어 버퍼 (여기서는 cube map) 에서 사용할 layer 를 선택함
        
        | **Face Value** | **Resulting Target** |
        | --- | --- |
        | 0 | `GL_TEXTURE_CUBEMAP_POSITIVE_X` |
        | 1 | `GL_TEXTURE_CUBEMAP_NEGATIVE_X` |
        | 2 | `GL_TEXTURE_CUBEMAP_POSITIVE_Y` |
        | 3 | `GL_TEXTURE_CUBEMAP_NEGATIVE_Y` |
        | 4 | `GL_TEXTURE_CUBEMAP_POSITIVE_Z` |
        | 5 | `GL_TEXTURE_CUBEMAP_NEGATIVE_Z` |
        - gl_Layer 를 사용해 cube 의 각각의 면의 좌표를 fragment shader 로 전달
        
        ```cpp
        #version 400 core
        layout (triangles) in;
        layout (triangle_strip, max_vertices=18) out;
        
        uniform mat4 shadowMatrices[6];
        
        out vec4 FragPos; // FragPos from GS (output per emitvertex)
        
        void main()
        {
            for(int face = 0; face < 6; ++face)
            {
                gl_Layer = face; // built-in variable that specifies to which face we render.
                for(int i = 0; i < 3; ++i) // for each triangle's vertices
                {
                    FragPos = gl_in[i].gl_Position;
                    gl_Position = shadowMatrices[face] * FragPos;
                    EmitVertex();
                }    
                EndPrimitive();
            }
        }
        ```
        
- fragment shader
    - FragPos 로 각 면의 좌표를 받은 뒤 해당 좌표를 0-1로 정규화 해 depth 를 기록함
    
    ```cpp
    #version 400 core
    in vec4 FragPos;
    
    uniform vec3 lightPos;
    uniform float far_plane;
    
    void main()
    {
        // get distance between fragment and light source
        float lightDistance = length(FragPos.xyz - lightPos);
        
        // map to [0;1] range by dividing by far_plane
        lightDistance = lightDistance / far_plane;
        
        // write this as modified depth
        gl_FragDepth = lightDistance;
    }
    ```
    

## shadow map

- vertex shader

```cpp
#version 400 core

layout(location = 0) in vec3 VERTEX;
layout(location = 1) in vec3 aNORMAL;

uniform mat4 MODEL;

layout (std140) uniform Matrices
{
    mat4 PROJECTION;
    mat4 VIEW;
};

out vec3 NORMAL;
out vec3 POSITION;

void main(){

	mat4 MVP = PROJECTION * VIEW * MODEL;
	gl_Position = MVP * vec4(VERTEX, 1);

	NORMAL = transpose(inverse(mat3(MODEL))) * aNORMAL;

	POSITION = vec3(MODEL * vec4(VERTEX, 1));
}
```

- fragment shader

```cpp
#version 400 core

in vec3 NORMAL;
in vec3 POSITION;

out vec4 color;

uniform vec3 COLOR;
uniform vec3 lightPos;
uniform float far_plane;
uniform vec3 VIEWPOS;

uniform samplerCube shadowMap;

vec3 sampleOffsetDirections[20] = vec3[]
(
    vec3( 1,  1,  1), vec3( 1, -1,  1), vec3(-1, -1,  1), vec3(-1,  1,  1), 
    vec3( 1,  1, -1), vec3( 1, -1, -1), vec3(-1, -1, -1), vec3(-1,  1, -1),
    vec3( 1,  1,  0), vec3( 1, -1,  0), vec3(-1, -1,  0), vec3(-1,  1,  0),
    vec3( 1,  0,  1), vec3(-1,  0,  1), vec3( 1,  0, -1), vec3(-1,  0, -1),
    vec3( 0,  1,  1), vec3( 0, -1,  1), vec3( 0, -1, -1), vec3( 0,  1, -1)
);  

float calcShadow(vec3 fragPos) {
    vec3 fragToLight = fragPos - lightPos;
	  float closestDepth = texture(shadowMap, fragToLight).r;
    closestDepth *= far_plane;
    float currentDepth = length(fragToLight);

    float bias = 0.05; 
    float shadow = currentDepth -  bias > closestDepth ? 1.0 : 0.0;

    return shadow;
}

void main(){
    vec3 normal = normalize(NORMAL);
    vec3 lightDir = normalize(lightPos - POSITION);
    float light = max(dot(lightDir, normal), 0.0);

	float ambient = 0.1;

	float shadow = calcShadow(POSITION);

	color = vec4(COLOR * (light * (1.0 - shadow) + ambient), 1);
}
```

### PCF

```cpp
float calcShadow(vec3 fragPos) {
	vec3 fragToLight = fragPos - lightPos;
	float currentDepth = length(fragToLight);
	float shadow  = 0.0;
	float bias    = 0.05; 
	float samples = 4.0;
	float offset  = 0.1;
	for(float x = -offset; x < offset; x += offset / (samples * 0.5))
	{
	    for(float y = -offset; y < offset; y += offset / (samples * 0.5))
	    {
	        for(float z = -offset; z < offset; z += offset / (samples * 0.5))
	        {
	            float closestDepth = texture(shadowMap, fragToLight + vec3(x, y, z)).r; 
	            closestDepth *= far_plane;   // Undo mapping [0;1]
	            if(currentDepth - bias > closestDepth)
	                shadow += 1.0;
	        }
	    }
	}
	shadow /= (samples * samples * samples);
	
	return shadow;
}
```

- 주변을 샘플링하여 평균화 해 그림자 테두리의 지그재그모양을 해결
    - 위처럼 하게되면 너무 많은 샘플링을 할수있게되기 때문에 offset 과 samples 를 조절해 최적화를 진행

```cpp
vec3 sampleOffsetDirections[20] = vec3[]
(
    vec3( 1,  1,  1), vec3( 1, -1,  1), vec3(-1, -1,  1), vec3(-1,  1,  1), 
    vec3( 1,  1, -1), vec3( 1, -1, -1), vec3(-1, -1, -1), vec3(-1,  1, -1),
    vec3( 1,  1,  0), vec3( 1, -1,  0), vec3(-1, -1,  0), vec3(-1,  1,  0),
    vec3( 1,  0,  1), vec3(-1,  0,  1), vec3( 1,  0, -1), vec3(-1,  0, -1),
    vec3( 0,  1,  1), vec3( 0, -1,  1), vec3( 0, -1, -1), vec3( 0,  1, -1)
);

float calcShadow(vec3 fragPos) {
  vec3 fragToLight = fragPos - lightPos;
  float currentDepth = length(fragToLight);
  float shadow = 0.0;
  float bias   = 0.15;
  int samples  = 20;
  float viewDistance = length(VIEWPOS - fragPos);
  float diskRadius = (1.0 + (viewDistance / far_plane)) / 25.0;
  // float diskRadius = 0.01;
  for(int i = 0; i < samples; ++i)
  {
      float closestDepth = texture(shadowMap, fragToLight + sampleOffsetDirections[i] * diskRadius).r;
      closestDepth *= far_plane;
      if(currentDepth - bias > closestDepth)
          shadow += 1.0;
  }
  shadow /= float(samples);

  return shadow;
}
```

- 혹은 미리 샘플링 할 오프셋들을 지정해 정해진 범위를 샘플링하여 평균을 냄

```cpp
float diskRadius = (1.0 + (viewDistance / far_plane)) / 25.0;
```

- 추가로 diskRadius (샘플링하는 범위) 를 거리에 따라 조절해 멀수록 흐린 그림자를 만듬